import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.3.72"
    id "maven-publish"
    id "org.jetbrains.dokka" version "0.10.1"
}

group = "me.syari.ss.core"
version = "2.5"

repositories {
    mavenCentral()
    maven {
        url "https://papermc.io/repo/repository/maven-public/"
    }
    jcenter()
}

dependencies {
    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
    implementation("com.destroystokyo.paper:paper-api:1.15.2-R0.1-SNAPSHOT")
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [version: version]
    }
}

jar {
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava.srcDirs
}

dokka {
    outputFormat = "html"
    outputDirectory = "$buildDir/kdoc"
}

task dokkaJar(type: Jar, dependsOn: "dokka") {
    from fileTree(new File(project.buildDir, "kdoc"))
}

publishing {
    repositories {
        maven {
            url ssMavenRepoUpload
            credentials {
                username ssMavenRepoUploadUser
                password ssMavenRepoUploadPassword
            }
        }
    }
    publications {
        maven(MavenPublication) {
            from components.java
            artifact dokkaJar {
                classifier "kdoc"
            }
        }
    }
}